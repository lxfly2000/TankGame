SetCompressor lzma
Unicode true

!include "x64.nsh"

; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "TankGame"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "lxfly2000"
!define PRODUCT_WEB_SITE "https://github.com/lxfly2000/TankGame"

; MUI 1.67 compatible ------
!include "MUI2.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "SimpChinese"

; MUI end ------

RequestExecutionLevel user
ManifestDPIAware true

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "TankGameI.exe"
InstallDir "$TEMP\${PRODUCT_NAME}"
ShowInstDetails show
ShowUnInstDetails show

VIProductVersion "1.0.0.1"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "ProductName" "${PRODUCT_NAME}"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "Comments" "${PRODUCT_NAME} 安装程序"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "CompanyName" "${PRODUCT_PUBLISHER}"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "LegalTrademarks" "${PRODUCT_NAME} 是 ${PRODUCT_PUBLISHER} 的合法商标。"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "LegalCopyright" "Copyright (C) 2019 ${PRODUCT_PUBLISHER}, 本程序发布网址：${PRODUCT_WEB_SITE}"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "FileDescription" "${PRODUCT_NAME} 安装程序"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "FileVersion" "1.0.0.0"

Function CheckVCRedist
  Pop $0
  Pop $1
  Pop $2
  IfFileExists "$SYSDIR\$0" 0 +2
  Return
  NSISdl::download /TRANSLATE2 "正在下载 %s，请等待完成后选择“是”允许运行……" "连接中……" "（剩余1秒）" "（剩余1分钟）" "（剩余1小时）" "（剩余%u秒）" "（剩余%u分钟）" "（剩余%u小时）" "已下载 %s KB（%d%%），总计 %s KB，速度 %u.%01u KB/秒" /TIMEOUT=10000 $1 "$INSTDIR\$2"
  Pop $R0 ;Get the return value
  StrCmp $R0 "success" 0 +2
  ExecShellWait "open" "$INSTDIR\$2" "/passive /norestart"
FunctionEnd

Section "MainSection" SEC01
  ${DisableX64FSRedirection}
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "fmod64.dll"
  File "opencv_world401.dll"
  File "TankGame.exe"
  File "控制.txt"
  File /r "bgm"
  File /r "pic"
  File /r "se"
  SetAutoClose true

  Push "vc_redist.x64.exe"
  Push "http://download.visualstudio.microsoft.com/download/pr/9e04d214-5a9d-4515-9960-3d71398d98c3/1e1e62ab57bbb4bf5199e8ce88f040be/vc_redist.x64.exe"
  Push "vcruntime140.dll"
  Call CheckVCRedist
  Push "vc_redist.x64.exe"
  Push "http://download.visualstudio.microsoft.com/download/pr/9e04d214-5a9d-4515-9960-3d71398d98c3/1e1e62ab57bbb4bf5199e8ce88f040be/vc_redist.x64.exe"
  Push "msvcp140.dll"
  Call CheckVCRedist
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  Exec '"$INSTDIR\uninst.exe" /PreExec=TankGame.exe'
SectionEnd


!include "FileFunc.nsh"

Function un.onInit
  ${GetParameters} $R0
  ${GetOptions} $R0 "/PreExec=" $R0
  IfErrors +2
  ExecShellWait "" "$INSTDIR\$R0"
FunctionEnd

Section Uninstall
  RMDir /r "$INSTDIR"

  SetAutoClose true
SectionEnd
